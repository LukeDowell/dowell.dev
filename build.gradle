plugins {
    id "com.moowork.node" version "1.3.1" // Allows for node download & execution via gradle
    id 'org.springframework.boot' version '2.1.4.RELEASE'
    id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'dev.dowell'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

node {
    download = true

    // Set the work directory for unpacking node
    workDir = file("${project.buildDir}/nodejs")

    // Set the work directory for NPM
    npmWorkDir = file("${project.buildDir}/npm")
}

task appNpmInstall(type: NpmTask) {
    description = "Installs all dependencies from package.json"
    workingDir = file("${project.projectDir}/webapp")
    args = ["install"]
}

task appNpmBuild(type: NpmTask) {
    description = "Builds production version of the webapp"
    workingDir = file("${project.projectDir}/webapp")
    args = ["run", "build"]
}

task appNpmTest(type: NpmTask) {
    description = "Runs webapp tests"
    workingDir = file("${project.projectDir}/webapp")
    args = ["test"]
}

task copyWebApp(type: Copy) {
    from 'webapp/build'
    into 'build/resources/main/static/.'
}

appNpmBuild.dependsOn appNpmInstall
appNpmTest.dependsOn appNpmBuild
copyWebApp.dependsOn appNpmTest
bootJar.dependsOn copyWebApp
